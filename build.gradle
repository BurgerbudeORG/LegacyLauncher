wrapper {
    gradleVersion = "6.1"
}

import java.text.SimpleDateFormat

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'eclipse'

repositories {
    mavenCentral()
}

group = 'net.minecraft'
archivesBaseName = 'launchwrapper'
version = '2.0'
sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile('net.sf.jopt-simple:jopt-simple:4.5')
    compile('org.apache.logging.log4j:log4j-api:2.11.0')
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
}

jar {
    manifest {
        attributes(
                'Implementation-Version': version,
                'Implementation-Vendor': "BurgerbudeORG",
                'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Created-By': "Gradle ${gradle.gradleVersion}"
        )
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version

            from components.java
        }
    }
}